plugins {

  id 'groovy'
  id 'maven'
  id 'eclipse'
  id 'idea'
  id 'project-report'

  id "net.saliman.cobertura" version "2.2.5"
  id "com.github.hierynomus.license" version "0.11.0"
  id "org.ajoberstar.github-pages" version "0.10.0"
  id "org.asciidoctor.gradle.asciidoctor" version "1.5.0"

}

group = 'com.aestasit.infrastructure.winrm'
version = '0.6'

apply from: 'gradle/repos.gradle'
apply from: 'gradle/sonar.gradle'
apply from: 'gradle/docs.gradle'

dependencies {

  compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
  compile "org.slf4j:slf4j-api:${slf4jVersion}"
  compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
  compile "ch.qos.logback:logback-classic:${logbackVersion}"
  compile "org.codehaus.groovy.modules.http-builder:http-builder:${httpBuilderVersion}"
  compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
  compile "commons-io:commons-io:${commonsIOVersion}"
  compile "commons-codec:commons-codec:${commonsCodecVersion}"

  testCompile "junit:junit:${junitVersion}"
  testCompile "org.mortbay.jetty:jetty:${jettyVersion}"

}

sourceSets {
  integrationTest {
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

configurations {
  all*.exclude group: 'commons-logging'
  all*.exclude group: 'log4j'
  all*.exclude module: 'slf4j-simple'
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

task integration(type: Test) {
  description = "Run integration tests (located in src/integrationTest/groovy)."
  testClassesDir = project.sourceSets.integrationTest.output.classesDir
  classpath = project.sourceSets.integrationTest.runtimeClasspath
}

idea {
  module {
    testSourceDirs += file('src/integrationTest/groovy')
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.2'
}

license {
  header rootProject.file('HEADER')
}
